#!/bin/csh -f

### wien2wannier/SRC/w2wpara_lapw
###
###    Wrapper script for ‘w2w’ when using ‘vector’ files from a
###    parallel calculation
###
### Copyright 2014-2015 Elias Assmann

onintr exit
set name	= $0
set bin		= $name:h	#default directory for WIEN-executables
if !(-d $bin) set bin = .

if ($#argv < 1) then
  echo usage: $0 deffile
  exit
endif

set updn cmplx so

while ($#argv)
  switch ($1)
  case -up:
    set updn = -up
    shift; breaksw
  case -dn:
    set updn = -dn
    shift; breaksw
  case -so:
    set so   = so
    shift; breaksw
  case -c:
    set cmplx = c
    shift; breaksw
  default:
    set def = $1:r
    shift; breaksw
  endsw
end

set exe = $bin/w2w$cmplx

set maxproc = `grep : .processes | grep  -v -e init: -e residue: | wc -l`

if (! $?so) $bin/vec2old -p -local $updn
$exe $def.def $maxproc
exit 0



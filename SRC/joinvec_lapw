#!/bin/csh -f

### wien2wannier/SRC/joinvec_lapw
###
###    Wrapper script for ‘join_vectorfiles’
###
### Copyright 2014-2016 Elias Assmann

onintr exit
set name	= $0
set bin		= $name:h	#default directory for WIEN-executables
if !(-d $bin) set bin = .

if ($#argv < 1) then
  echo usage: $0 deffile
  exit
endif

set updn cmplx so

while ($#argv)
  switch ($1)
  case -up:
    set updn = -up
    shift; breaksw
  case -dn:
    set updn = -dn
    shift; breaksw
  case -so:
    set so   = so
    shift; breaksw
  case -c:
    set cmplx = -c
    shift; breaksw
  default:
    set def = $1:r
    shift; breaksw
  endsw
end

set exe = $bin/join_vectorfiles

set maxproc = `grep : .processes | grep  -v -e init: -e residue: | wc -l`

## Fetch vector files if needed
set vector=`grep -c '^ *10 *,' $def.def`
# vec2old does not work on so
if ($vector && $?so) $bin/vec2old -p -local $updn
$exe $def.def $maxproc $cmplx
exit 0

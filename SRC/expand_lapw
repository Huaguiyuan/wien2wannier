#!/bin/csh -f

### wien2wannier/SRC/expand_lapw
###
###    This is the wien2wannier version of expand_lapw.

set name  = $0
set bin   = $name:h

if !($#argv == 0) then
    if ($1 == -h || $1 == -H) then
	echo "expand_lapw expands all WIEN tar-files in the current subdir"
	exit(1)
    endif
endif

#
echo "WARNING: This will expand all WIEN.tar files \!\!\!"
echo "It will overwrite all changes you have made previously in the WIEN subdirectories"
echo "continue (y/n) "
set b=($<)
if ($b == 'y') then
   foreach i ( SRC*.tar TiC.tar fccni.tar TiO2.tar example_struct_files.tar )

    if (-e $i) then
	echo "tar -xf $i"
	tar -xf $i
    endif
   end


cp SRC/VERSION .

foreach i ( SRC/*_lapw )
	  if (-e $i) then
	    cp $i .
            set fn = `echo $i:t |sed "s/_lapw//"`
            if (-e $fn) rm $fn
            ln -s $i:t $fn
            echo "$i   copied and linked to   $fn"
	  else
	    echo "$i not found\!"
	  endif
end


cd SRC_templates
foreach i ( case.* )
	  if (-e $i) then
            set fn = `echo template.$i:e`
            if (-e $fn) rm $fn
            ln -s $i $fn
            echo "$i   linked to   $fn"
	  else
	    echo "$i not found\!"
	  endif
end
cd ..

#if ( -e wien ) rm wien
#    ln -s SRC_wienbox/wien wien

if ( -e w2web ) rm w2web
    ln -s SRC_w2web/w2web w2web
if ( -e SRC_w2web/htdocs/usersguide) then
     rm -r SRC_w2web/htdocs/usersguide
endif
    ln -s $PWD/SRC_usersguide_html SRC_w2web/htdocs/usersguide
    cd $PWD/SRC_usersguide_html
    ln -s ../SRC/usersguide.pdf .
    cd ..

foreach i ( lapw1 lapw2 lapwso optic lapwdm hf w2w wplot )
    if (-e ${i}cpara ) rm ${i}cpara
    ln -s ${i}para ${i}cpara
end

cd SRC_structeditor
ln -s ../SRC_lib .
cd ..

if(-e INSTALLDATE) rm INSTALLDATE

chmod +x siteconfig_lapw

#Berry-PI
cd SRC_BerryPI/BerryPI
set pythonpath=`which python2.7`
if($status == 1 ) then
     echo python2.7 not found in your path
     set pythonpath=`which python`
     if($status == 1 ) then
       set pythonpath=/usr/bin/python
       echo python not found in your path
       echo BerryPI will not work without Python
       echo install python2.7 yourself and configure $PWD/config.py
     else
       echo python found at $pythonpath.
       echo BerryPI requires Python2.7, make sure your Python version is 2.7 or higher.
     endif
else
     set numpyver=`$pythonpath -c 'import numpy; print numpy.__version__'`
     if("$numpyver" != "1.6.2") then
        echo NumPy-1.6.2 not found, instead you have: $numpyver
        echo "BerryPI will not work without NumPy(-1.6.2)"
     endif
endif
sed -e "s?DEFAULT_BIN_PATH=.*"'$'"?DEFAULT_BIN_PATH=$PWD?" <config.py >tmp
sed -e "s?DEFAULT_PYTHON_PATH=.*"'$'"?DEFAULT_PYTHON_PATH=$pythonpath?" <tmp >config.py
echo "configuring Berry-PI (config.py):"
echo setting   DEFAULT_BIN_PATH to $PWD
echo and       DEFAULT_PYTHON_PATH to $pythonpath
cd ../..
#end Berry-PI

cat<<EOF

WIEN is now expanded. The shell-script commands were copied and links created.
To configure your Fortran-executables run:

./siteconfig_lapw


EOF

endif

exit(0)


## Time-stamp: <2015-12-21 13:53:05 assman@faepop36.tu-graz.ac.at>
